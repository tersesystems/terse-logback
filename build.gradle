plugins {
    // http://andresalmiray.com/an-opinionated-way-to-build-gradle-projects/
    // https://aalmiray.github.io/kordamp-gradle-plugins/#_project_structure
    id 'org.kordamp.gradle.project' version '0.14.0'            
    id 'org.kordamp.gradle.bintray' version '0.14.0'     
    id 'com.gradle.build-scan'      version '2.2.1'       
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey'))   ext.bintrayApiKey = ''
ext.travis = (System.env['TRAVIS'] ?: false) as boolean
ext.publishing = (System.env['PUBLISH'] ?: false) as boolean

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name          = rootProject.name
        vendor        = 'tersesystems'
        description   = 'Terse Logback'
        inceptionYear = '2018'
        tags          = ['logging', 'logback']

        links {
            website      = "https://github.com/tersesystem/${rootProject.name}"
            issueTracker = "https://github.com/tersesystem/${rootProject.name}/issues"
            scm          = "https://github.com/tersesystem/${rootProject.name}.git"
        }

        people {
            person {
                id    = 'wsargent'
                name  = 'Will Sargent'
                roles = ['developer']
            }
        }
    }

    license {
        licenses {
            license {
                id = 'CC0-1.0'
            }
        }
    }

    bintray {
        credentials {
            // http://andresalmiray.com/gaining-insight-into-a-gradle-build/
            username = project.bintrayUsername
            password = project.bintrayApiKey
        }
        userOrg    = 'tersesystems'
        name       = rootProject.name
    }

}

allprojects {
    apply plugin: 'idea'

    repositories {
        jcenter()
    }
}

def String artifactToJavadoc(String organization, String name, String apiVersion) {
    String slashedOrg = organization.replace('.', '/')
    "https://oss.sonatype.org/service/local/repositories/releases/archive/$slashedOrg/$name/$apiVersion/$name-$apiVersion-javadoc.jar/!/"
}

def String jvmToJavadoc() {
    // No simple way to set up javadoc external links to JDK in Gradle
    // Should select on version:

    'https://docs.oracle.com/javase/8/docs/api/'
    // https://docs.oracle.com/javase/9/docs/api/
    // https://docs.oracle.com/javase/10/docs/api/
    // https://docs.oracle.com/en/java/javase/11/docs/api/
}

subprojects { subproj ->
    apply plugin: 'java'

    if (!subproj.name.contains('example')) {
        // Allow signing with the Yubikey using gpg-agent
        if (publishing && ! travis) {
            config {
                bintray { enabled = true }
            }

            signing {
                useGpgCmd()
                sign configurations.archives
            }
        }
    }

    config {
        info {
            description = project.project_description
        }

        // Sets up javadoc for individual modules
        javadoc {
            options.charSet = 'UTF-8'
            options.encoding = 'UTF-8'
            options.docEncoding = 'UTF-8'
            options.use = true          
            options.links = [
              jvmToJavadoc(),
              // XXX How do I get my source library path to Javadoc?
              artifactToJavadoc('org.slf4j', 'slf4j-api', '1.7.25'),
              artifactToJavadoc('ch.qos.logback', 'logback-classic', '1.2.3')
            ]
        }
    }

    dependencies {
        testCompile 'org.assertj:assertj-core:3.8.0'
        testCompile "junit:junit:$junitVersion"
        testCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
        testCompile "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
        testCompile "org.junit.vintage:junit-vintage-engine:$junitVintageVersion"
    }

    test {
        useJUnitPlatform()
    }
}
