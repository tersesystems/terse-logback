/*
 * SPDX-License-Identifier: CC0-1.0
 *
 * Copyright 2018-2019 Will Sargent.
 *
 * Licensed under the CC0 Public Domain Dedication;
 * You may obtain a copy of the License at
 *
 *     http://creativecommons.org/publicdomain/zero/1.0/
 */
plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

dependencies {
    implementation "net.bytebuddy:byte-buddy:$bytebuddyVersion"
    implementation "com.typesafe:config:${configVersion}"

    // We cannot allow dependencies from logback-tracing in here.
    shadow project(":logback-tracing")
    shadow "org.slf4j:slf4j-api:$slf4jVersion"
    shadow "ch.qos.logback:logback-classic:$logbackVersion"
    shadow "ch.qos.logback:logback-core:$logbackVersion"
    shadow "net.logstash.logback:logstash-logback-encoder:$logstashVersion"
    shadow "com.google.auto.value:auto-value-annotations:1.6.2"
    shadow group: 'com.fasterxml.uuid', name: 'java-uuid-generator', version: '3.2.0'

    // You'll need this, but better to use the version already defined in project
    testImplementation "net.bytebuddy:byte-buddy-agent:$bytebuddyVersion"
    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
}

shadowJar {
    classifier null

    // Shade typesafe config and bytebuddy itself since this is used in bootstrap classloader
    // and so would be visible to everything downstream
    relocate 'com.typesafe', 'com.tersesystems.logback.shadow.typesafe'
    relocate 'net.bytebuddy', 'com.tersesystems.logback.shadow.bytebuddy'

    exclude '**/module-info.class'

    dependencies {
        exclude(dependency("com.fasterxml.jackson.core:"))
        exclude(dependency("com.fasterxml.jackson.annotation:"))
        exclude(dependency("com.fasterxml.jackson.databind:"))
        exclude(dependency("net.logstash.logback:logstash-logback-encoder:$logstashVersion"))
        exclude(dependency("org.slf4j:slf4j-api:$slf4jVersion"))
    }
}


// https://github.com/serenity-bdd/serenity-cli/blob/7c7bf7791fe263818585fda16c8dab021ad17b3c/build.gradle
publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

jar {
    archiveClassifier.set 'unbundled'

    manifest {
        attributes(
            "Agent-Class": "com.tersesystems.logback.bytebuddy.LogbackInstrumentationAgent",
            "Premain-Class": "com.tersesystems.logback.bytebuddy.LogbackInstrumentationAgent",
            "Can-Redefine-Classes": true,
            "Can-Retransform-Classes": true
        )
    }
}

tasks.withType(Test).configureEach {
    doFirst {
        //jvmArgs "-javaagent:${shadowJar.archivePath}"
    }
}

// I had trouble getting a working example in Gradle using a fresh JVM and an agent, so I cheated.
// Here's what I did instead:
//
// gradle distZip
// cd logback-bytebuddy/build/distributions
// unzip logback-bytebuddy-0.0.0.zip
// cd logback-bytebuddy-0.0.0
// bin/logback-bytebuddy
//application {
//    mainClassName = 'com.tersesystems.logback.bytebuddy.PreloadedInstrumentationExample'
//    applicationDefaultJvmArgs = [
//            "-Dconfig.file=$buildDir/../src/test/resources/application.conf",
//            "-Dlogback.configurationFile=$buildDir/../src/test/resources/logback-test.xml",
//            '-javaagent:lib/logback-bytebuddy-0.0.0.jar',
//    ]
//}