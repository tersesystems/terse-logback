/*
 * SPDX-License-Identifier: CC0-1.0
 *
 * Copyright 2018-2020 Will Sargent.
 *
 * Licensed under the CC0 Public Domain Dedication;
 * You may obtain a copy of the License at
 *
 *  http://creativecommons.org/publicdomain/zero/1.0/
 */
plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

dependencies {
    implementation "net.bytebuddy:byte-buddy:$bytebuddyVersion"
    implementation "com.typesafe:config:${configVersion}"

    // We cannot allow dependencies from logback-tracing in here.
    shadow project(":logback-tracing")
    shadow "org.slf4j:slf4j-api:$slf4jVersion"
    shadow "ch.qos.logback:logback-classic:$logbackVersion"
    shadow "ch.qos.logback:logback-core:$logbackVersion"
    shadow "net.logstash.logback:logstash-logback-encoder:$logstashVersion"
    shadow "com.google.auto.value:auto-value-annotations:1.6.2"
    shadow group: 'com.fasterxml.uuid', name: 'java-uuid-generator', version: '3.2.0'

    // You'll need this, but better to use the version already defined in project
    testImplementation "net.bytebuddy:byte-buddy-agent:$bytebuddyVersion"
    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
}

jar {
    manifest {
        attributes "Premain-Class": "com.tersesystems.logback.bytebuddy.LogbackInstrumentationAgent"
        attributes "Agent-Class": "com.tersesystems.logback.bytebuddy.LogbackInstrumentationAgent"
        attributes "Can-Redefine-Classes": "true"
        attributes "Can-Retransform-Classes": "true"
    }
}

shadowJar {
    archiveFileName = "$archiveBaseName-$archiveVersion.$archiveExtension"

    // Shade typesafe config and bytebuddy itself since this is used in bootstrap classloader
    // and so would be visible to everything downstream
    relocate 'com.typesafe', 'com.tersesystems.logback.shadow.typesafe'
    relocate 'net.bytebuddy', 'com.tersesystems.logback.shadow.bytebuddy'

    exclude '**/module-info.class'

    dependencies {
        exclude(dependency("com.fasterxml.jackson.core:"))
        exclude(dependency("com.fasterxml.jackson.annotation:"))
        exclude(dependency("com.fasterxml.jackson.databind:"))
        exclude(dependency("net.logstash.logback:logstash-logback-encoder:$logstashVersion"))
        exclude(dependency("org.slf4j:slf4j-api:$slf4jVersion"))
    }
}

artifacts {
    archives shadowJar
}