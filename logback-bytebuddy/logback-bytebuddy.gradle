plugins {
    id 'application'
}

dependencies {
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "net.bytebuddy:byte-buddy:$bytebuddyVersion"
    compile "net.bytebuddy:byte-buddy-agent:$bytebuddyVersion"
    compile group: 'com.typesafe', name: 'config', version: "$configVersion"
    compile "ch.qos.logback:logback-classic:$logbackVersion"
    compile "net.logstash.logback:logstash-logback-encoder:$logstashVersion"
}

jar {
    manifest {
        attributes(
        "Agent-Class": "com.tersesystems.logback.bytebuddy.LogbackInstrumentationAgent",
        "Can-Redefine-Classes": true,
        "Can-Retransform-Classes": true,
        "Boot-Class-Path": "byte-buddy-${bytebuddyVersion}.jar slf4j-api-${slf4jVersion}.jar config-${configVersion}.jar logback-core-${logbackVersion}.jar logback-classic-${logbackVersion}.jar logstash-logback-encoder-${logstashVersion}.jar logback-bytebuddy-${version}.jar",
        "Premain-Class": "com.tersesystems.logback.bytebuddy.LogbackInstrumentationAgent"
        )
    }
}

// I had trouble getting a working example in Gradle using a fresh JVM and an agent, so I cheated.
// Here's what I did instead:
//
// gradle distZip
// cd logback-bytebuddy/build/distributions
// unzip logback-bytebuddy-0.0.0.zip
// cd logback-bytebuddy-0.0.0
// bin/logback-bytebuddy
application {
    mainClassName = 'com.tersesystems.logback.bytebuddy.PreloadedInstrumentationExample'
    applicationDefaultJvmArgs = [
            "-Dconfig.file=$buildDir/../src/test/resources/application.conf",
            "-Dlogback.configurationFile=$buildDir/../src/test/resources/logback-test.xml",
            '-javaagent:lib/logback-bytebuddy-0.0.0.jar',
    ]
}

config {
    javadoc {
        options.charSet = 'UTF-8'
        options.encoding = 'UTF-8'
        options.docEncoding = 'UTF-8'
        options.use = true
        options.links = [jvmToJavadoc(targetCompatibility)] + javadocFromDependencies(configurations.compile)
    }
}
